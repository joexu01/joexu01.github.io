> 作业1：完成一个文档，解释引导程序；
>
> 要求：
>
> 1、程序的文字解释
>
> 2、流程图（要求画图前看看流程图的规范）
>
> 3、需要有INT 10和INT 18的说明
>
> 4、注意、注意、注意：文档名规范：学号+姓名



# 系统与数据恢复技术作业1

2017060202001 许思博

[toc]

[INT 18](#int18)  [INT 13](#int13) [INT 10](#int10)

## “标准”MBR代码^[1]^

```assembly
 Absolute Sector 0 (Cylinder 0, Head 0, Sector 1)

        0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
 0000  FA 33 C0 8E D0 BC 00 7C 8B F4 50 07 50 1F FB FC  .3.....|..P.P... 
 0010  BF 00 06 B9 00 01 F2 A5 EA 1D 06 00 00 BE BE 07  ................ 
 0020  B3 04 80 3C 80 74 0E 80 3C 00 75 1C 83 C6 10 FE  ...<.t..<.u..... 
 0030  CB 75 EF CD 18 8B 14 8B 4C 02 8B EE 83 C6 10 FE  .u......L....... 
 0040  CB 74 1A 80 3C 00 74 F4 BE 8B 06 AC 3C 00 74 0B  .t..<.t.....<.t.
 0050  56 BB 07 00 B4 0E CD 10 5E EB F0 EB FE BF 05 00  V.......^.......
 0060  BB 00 7C B8 01 02 57 CD 13 5F 73 0C 33 C0 CD 13  ..|...W.._s.3...
 0070  4F 75 ED BE A3 06 EB D3 BE C2 06 BF FE 7D 81 3D  Ou...........}.=
 0080  55 AA 75 C7 8B F5 EA 00 7C 00 00 49 6E 76 61 6C  U.u.....|..Inval
 0090  69 64 20 70 61 72 74 69 74 69 6F 6E 20 74 61 62  id partition tab
 00A0  6C 65 00 45 72 72 6F 72 20 6C 6F 61 64 69 6E 67  le.Error loading
 00B0  20 6F 70 65 72 61 74 69 6E 67 20 73 79 73 74 65   operating syste
 00C0  6D 00 4D 69 73 73 69 6E 67 20 6F 70 65 72 61 74  m.Missing operat
 00D0  69 6E 67 20 73 79 73 74 65 6D 00 00 00 00 00 00  ing system......
 00E0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 00F0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0100  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0110  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0120  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0130  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0140  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0150  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0160  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0170  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0180  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 0190  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 01A0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
 01B0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 80 01  ................
 01C0  01 00 0B 7F BF FD 3F 00 00 00 C1 40 5E 00 00 00  ......?....@^... 
 01D0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................ 
 01E0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................ 
 01F0  00 00 00 00 00 00 00 00 00 00 00 00 00 00 55 AA  ..............U. 
        0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
```

## 在MBR执行之前

依靠BIOS闪存而不遵循新的EFI标准启动的计算机在开机时会执行BIOS代码，这些代码执行一系列例行程序，如加电自检^[2]^等。

然后，BIOS尝试查找可引导设备。如果该可引导设备是磁盘驱动器，则BIOS读取磁盘`0磁道`，`0柱面`，`1扇区`。如果其中包含有效的MBR（也就是说该扇区以0xAA55结束），BIOS便将该段代码加载至内存中，一般来说，代码将被加载到实模式地址0x0000:0x7C00中。接着内存地址被加载到指令寄存器，CPU开始执行MBR代码。

在微软世界中，MBR代码的作用是找到活动分区，执行一个引导程序，然后将找到的Windows的`bootmgr`程序加载到内存中。

## MBR代码分析

### Ⅰ. 00H--8AH 可执行代码

512字节扇区的前139个字节是可执行代码，我们先来分析00H--18H的代码做了些什么。我们知道，执行POST后，BIOS代码会将该扇区的代码加载到0x7C00，然后执行`JMP 0000:7C00`。该段代码数显将自身复制到内存的`0000:0600`位置，这一步是非常必要的，因为MBR代码随后会将活动分区的启动扇区装载进内存的相同位置`0000:7C00`。

* 接下来我们看一下第一部分反汇编代码：

> 在接下来的代码块中：
>
> | 内存位置 | 十六进制 | 汇编指令 | 注释   |
> | -------- | -------- | -------- | ------ |
> | 第一列   | 第二列   | 第三列   | 第四列 |

```assembly
7C00 FA            CLI                  ; 关闭可屏蔽中断
7C01 33C0          XOR	AX,AX           ; AX寄存器清零
7C03 8ED0          MOV	SS,AX           ; 堆栈段寄存器清零
7C05 BC007C        MOV	SP,7C00	        ; 堆栈指针设定为0x7C00
7C08 8BF4          MOV	SI,SP           ; 源变址寄存器：从SP指向的地址开始复制
7C0A 50            PUSH	AX
7C0B 07            POP	ES              ; 清零附加段寄存器  AX = 0
7C0C 50            PUSH	AX
7C0D 1F            POP	DS              ; 清零数据段寄存器
7C0E FB            STI                  ; 开中断

7C0F FC            CLD                  ; 方向位清零 DF=0，串操作时地址递增
7C10 BF0006        MOV	DI,0600         ; 目的变址寄存器改为0x0600
                                        ;   也就是该份代码副本的起始位置
7C13 B90001        MOV	CX,0100         ; 100H = 256D 转移256字的数据到
                                        ;   刚才的以0x0600为起始地址的内存区域
7C16 F2 (*)        REP (*)              ; 重复MOVSW直到CX寄存器为0
                                        ; 
7C17 A5            MOVSW                ; 每次复制2字节（1字）数据

7C18 EA1D060000    JMP	0000:061D       ; 跳转到代码新副本
```

*反汇编代码中REP：[请参阅](https://thestarman.pcministry.com/asm/mbr/STDMBR.htm#REP)

> 上述代码第21行，JMP的目标地址是0000:061D，因为该指令长度为5字节，18H + 5H = 1DH

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200324205735502.png" alt="image-20200324205735502" style="zoom: 33%;" />



* 接下来，代码将会尝试在分区表中找到一个活动的入口（表明该分区可以启动，bootable）。一个分区入口（partition entry）的第一个字节被用来标识该分区时是否可以启动。如果第一个字节是`80H`则表明该分区可以启动，如果是`00H`则标识表明该分区不能启动。[点击这里](#partition_sample)查看示例分区表。

```assembly
061D BEBE07        MOV	SI,07BE           ; 分区表中第一个入口的位置
                                          ;   点击上面链接跳转到示例分区表
0620 B304          MOV	BL,04             ; 四个分区记录
0622 803C80        CMP	BYTE PTR [SI],80  ; 检查该字节是否为80H（是否可启动）
0625 740E          JE	0635              ; 是80H，ZF=1，跳转到下一个测试
0627 803C00        CMP	BYTE PTR [SI],00  ; 不是80H，再检测它是否为00H，若不是，则它
062A 751C          JNE	0648              ; 就是一无效的分区表，跳转到0x0648显示错误
062C 83C610        ADD	SI,+10            ; 检查下一个表项是否是活动分区
                                          ;   源变址寄存器+10h，也就是16Bytes
062F FECB          DEC	BL                ; 入口计数器减一（由0620处的指令初始化）
0631 75EF          JNZ	0622              ; 所有分区表项都被检查过了吗？
										  ;   如果未跳转说明全部测试完毕
0633 CD18          INT  18                ; 所有分区表项都不可启动，执行ROM-Basic
                                          ;   程序（只在某些IBM机上起作用）
                                          ;   很多BIOS只是简单地打印：
                                          ;   "PRESS A KEY TO REBOOT" 
                                          ;   当该中断执行时
```

> <span id='int18'>INT 18</span>^[3]^：在IBM兼容机上，该中断启动ROM Cassette Basic，当引导失败时执行此程序。（例：打印“Boot disk error. Replace disk and press any key to continue...”这类提示信息）

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200324211729435.png" alt="image-20200324211729435" style="zoom:50%;" />



* 找到一个活动分区，其他所有分区项的自举标志必须全为00H，否则出错。接下来的指令会将自举标志、起始磁头、扇区、柱面号复制到DX、CX寄存器中，为后面INT 13中断做准备。

```assembly
0635 8B14          MOV	DX,[SI]           ; 自举标志 -> DL; 起始磁头 -> DH
                                          ;   对于标准MBR代码来说，DL寄存器的值
          								  ;   永远是80H，也就是说只有第一个分区是活动的
         								  ;   但是这部分代码常常会被替换用来从其他驱动器
          								  ;   启动

0637 8B4C02        MOV	CX,[SI+02]        ; 扇区 -> CL / 柱面号 -> CH

063A 8BEE          MOV	BP,SI             ; 保存活动分区表项偏移量到BP寄存器
                                          ;   卷启动扇区(Volume Boot Sector)
063C 83C610        ADD	SI,+10            ; 源变址寄存器指向下16字节开头
063F FECB          DEC	BL                ; 最后一个分区表项？
0641 741A          JZ	065D              ; 所有分区入口检查完毕
                                          ;   -> 跳转到启动例程
0643 803C00        CMP	BYTE PTR [SI],00  ; 检查分区表项自举标志==00H？
0646 74F4          JE	063C              ; 是00H，跳转到下一入口
```

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200324214915161.png" alt="image-20200324214915161" style="zoom:50%;" />



* 在上述检查过程中，有入口的自举标志不为00H，下面的代码便打印错误信息，并将程序“锁死”，进入一个死循环。

```assembly
0648 BE8B06        MOV	SI,068B       ; SI寄存器指向的错误信息
									  ;   -> "Invalid partition table"
064B AC            LODSB              ; 把SI指向的内容加载到AL寄存器
                                      ;   SI寄存器值自增
064C 3C00          CMP	AL,00         ; AL是00H吗？
064E 740B          JE	065B          ; 如果是的则跳转，若不是则执行下面的指令
0650 56            PUSH	SI            ; 保存指针到栈
0651 BB0700        MOV	BX,0007       ; 10号中断的0E功能将字符打印在屏幕上
0654 B40E          MOV	AH,0E         
0656 CD10          INT	10            
0658 5E            POP	SI
0659 EBF0          JMP	064B

065B EBFE          JMP  065B          ; 死循环。必须关机或重启
```

> <span id='int10'>INT 10</span>^[3]^：10H中断是由BIOS对显示器和屏幕所提供的服务程序。使用INT10h服务程序时，必须先指定AH寄存器中的内容，以指定需要调用的功用。
>
> 0EH：在Teletype模式下显示字符

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200324222219453.png" alt="image-20200324222219453" style="zoom:50%;" />



* 好了，现在我们可以加载活动分区的第一个扇区了：

> 在大多数驱动器中，活动分区的第一扇区一般是磁盘的第63扇区，第2扇区到第62扇区通常为空，除非安装过大MBR，超大磁盘的磁盘翻译软件或多系统启动软件/安全加密启动代码。

```assembly
065D BF0500        MOV	DI,0005       ; 尝试5次（如果有必要的话）
0660 BB007C        MOV	BX,7C00       ; 把操作系统启动扇区加载到0000:7C00
0663 B80102        MOV	AX,0201       ; 调用功能02（AL），读取1个扇区（AH）
0666 57            PUSH	DI
0667 CD13          INT  13            ; 13号中断，读扇区
0669 5F            POP	DI
066A 730C          JNC	0678          ; 如果没有错误，CF清零；CF为0时跳转
066C 33C0          XOR	AX,AX         ; 出现错误，磁盘系统需要复位
066E CD13          INT  13            ;   使用00H功能
0670 4F            DEC	DI            ; 尝试次数计数器递减 (if > 0)
0671 75ED          JNZ	0660          ;   再次尝试

0673 BEA306        MOV	SI,06A3       ; 尝试次数用完: "Error loading
0676 EBD3          JMP	064B          ; operating system"进入死循环
```

> <span id='int13'>INT 13</span>^[4]^：13号中断——直接磁盘服务（Direct Disk Service）
>
> * 02H功能是读扇区，如果执行完**没有错误，CF被清零**
>
>   AL＝扇区数 ；CH＝柱面 ；CL＝扇区 ；DH＝磁头 
>
>   DL＝驱动器，00H--7FH：软盘；80H--0FFH：硬盘 
>
>   ES:BX＝缓冲区的地址 
>
>   出口参数：CF＝0——操作成功，AH＝00H，AL＝传输的扇区数，否则，AH＝状态代码，参见功能号01H中的说明 
>
> * 功能00H 
>
>   功能描述：磁盘系统复位 
>
>   入口参数：AH＝00H 
>
>   DL＝驱动器，00H--7FH：软盘；80H--0FFH：硬盘 
>
>   出口参数：CF＝0——操作成功，AH＝00H，否则，AH＝状态代码，参见功能号01H中的说明 

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200325003132071.png" alt="image-20200325003132071" style="zoom:50%;" />



* 活动分区的启动扇区被转载到内存中后，我们需要检查其是否有效：该检查非常简单，就看看这个扇区最后一个字是否为`0xAA55`。

```assembly
0678 BEC206        MOV	SI,06C2         ; -> "Missing operating system"
                                        ; SI指向该错误语句，以防启动扇区读取错误
067B BFFE7D        MOV	DI,7DFE         ; 指向启动扇区的最后一个字，它应该是AA55H

067E 813D55AA      CMP	WORD PTR [DI],AA55 ; 检查是否是AA55H
0682 75C7          JNE	064B               ; 如果不是，显示错误信息并锁死系统

0684 8BF5          MOV	SI,BP	        ; SI=BP ->  都是活动分区表项偏移量
                                        ; 被操作系统启动代码使用
0686 EA007C0000    JMP	0000:7C00       ; 跳转到操作系统启动代码
```

<img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200325004720685.png" alt="image-20200325004720685" style="zoom:50%;" />



### Ⅱ. 错误信息（Error Message）

```assembly
068B                                   49 6E 76 61 6C              Inval
0690  69 64 20 70 61 72 74 69-74 69 6F 6E 20 74 61 62   id partition tab
06A0  6C 65 00 45 72 72 6F 72-20 6C 6F 61 64 69 6E 67   le.Error loading
06B0  20 6F 70 65 72 61 74 69-6E 67 20 73 79 73 74 65    operating syste
06C0  6D 00 4D 69 73 73 69 6E-67 20 6F 70 65 72 61 74   m.Missing operat
06D0  69 6E 67 20 73 79 73 74-65 6D 00                  ing system.		
```



### Ⅲ. <span id='partition_sample'>分区表示例</span>

这是一个分区表示例。该分区表包含四个分区入口记录，每个记录长度16字节。

```assembly
07BE                                            80 01                 ..
07C0  01 00 06 3F 3F C4 3F 00-00 00 81 1E 0C 00 00 00   ...??.?.........
07D0  01 C5 05 3F 7F 47 C0 1E-0C 00 40 0F 08 00 00 00   ...?.G....@.....
07E0  41 48 82 3F 7F 53 00 2E-14 00 00 BD 00 00 00 00   AH.?.S..........
07F0  41 54 83 3F BF 0F 00 EB-14 00 00 91 0B 00 55 AA   AT.?..........U.
       0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
```

> 我们可以根据下面的表格逐一解释四个记录的内容：
>
> <img src="C:\Users\Joseph\AppData\Roaming\Typora\typora-user-images\image-20200323223417701.png" alt="image-20200323223417701" style="zoom:67%;" />

| 分区格式类型 | 活动分区 | 起始柱面/磁头/扇区 | 终止柱面/磁头/扇区 | 相对扇区数 | 本分区扇区总数 |
| ------------ | -------- | ------------------ | ------------------ | ---------- | -------------- |
| DOS FAT-16   | Y        | 0/1/1              | 196/63/63          | 63         | 794241         |
| Extended     | N        | 197/0/1            | 327/63/63          | 794304     | 528192         |
| Linux Swap   | N        | 328/0/1            | 339/63/63          | 1322496    | 48384          |
| Linux Ext2FS | N        | 340/0/1            | 527/63/63          | 1370880    | 758016         |



### Ⅳ. 填充字节

剩下的227个字节（从0xDB到0x1BD）全部为填充字节，值为0。

如果在偏移量0x1B8到0x1BB之间看到四个非零的十六进制字节，这通常是由于将驱动器放入使用Windows™ **NT**，**2000**或**XP**启动的计算机中造成的。这些字节称为**NT驱动器序列号**。

## 参考

[1] [An Examination of the Standard MBR](https://thestarman.pcministry.com/asm/mbr/STDMBR.htm#CHS)

[2] [Analysis of a Master Boot Record - EternalPetya | Count Upon Security](https://countuponsecurity.com/2017/07/02/analysis-of-a-master-boot-record-eternalpetya/)

[3] [BIOS Interrupt call - Wikipedia](https://en.wikipedia.org/wiki/BIOS_interrupt_call)

[4] [INT13中断详解](https://blog.csdn.net/brainkick/article/details/7583727)